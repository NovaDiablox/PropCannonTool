:: Hard reset the HEAD: git push origin +<NEW_HEAD_COMMIT_HASH>:master
:: 6af74344a888bdf9fbb35d887c3f77691820a50e
:: git push origin +6af74344a888bdf9fbb35d887c3f77691820a50e:master
:: cd /e
:: cd Documents/Lua-Projs/SVN/TrackAssemblyTool_GIT_master

@echo off

setlocal EnableDelayedExpansion

set gmadGitHEAD=
set gmadRevPath=%~dp0
set gmadNameLOG=gmad_log.txt
set gmadName=PropCannonTool
set gmadCommits=https://github.com/dvdvideo1234/%gmadName%/commit/
set "gmadPathGIT=C:\Program Files\Git\bin"
set gmadBinPath=D:\Games\Steam\steamapps\common\GarrysMod\bin
set gmadADTools=%gmadRevPath%data\propcannon\tools
set "gmadTime=%date% %time%"
set gmadID=286474801
set gmadDirs=(lua)

:: New line is actually needed when escaped

title Addon %gmadName% updater/publisher

echo Press Crtl+C to terminate !
echo Press a key if you do not want to wait !
echo Rinning in %gmadRevPath%.
echo Npp Find --\h{1,}\n-- replace --\n-- in dos format before commit !

:: Extract repository source contents
IF EXIST !gmadNameLOG! del !gmadNameLOG!
IF EXIST Workshop rd /S /Q Workshop

timeout 5

md %gmadRevPath%Workshop\!gmadName!>>!gmadNameLOG!
for %%i in %gmadDirs% do (
  echo Extracting %%i
  timeout 3
  xcopy !gmadRevPath!%%i !gmadRevPath!Workshop\!gmadName!\%%i /EXCLUDE:!gmadADTools!\workshop\key.txt /E /C /I /F /R /Y>>!gmadNameLOG!
)

call copy !gmadADTools!\workshop\addon.json !gmadRevPath!Workshop\!gmadName!\addon.json>>!gmadNameLOG!
call "!gmadBinPath!\gmad.exe" create -folder "!gmadRevPath!Workshop\!gmadName!" -out "!gmadRevPath!Workshop\!gmadName!.gma">>!gmadNameLOG!

:: Obtain the latest commit hash from the repository
call "!gmadPathGIT!\git.exe" rev-parse HEAD>!gmadNameLOG!
call set /p gmadGitHEAD=<!gmadNameLOG!
call del !gmadRevPath!!gmadNameLOG!

:: Obtain the log message from the latest revision
call echo !gmadTime!>!gmadNameLOG!
call echo.>>!gmadNameLOG!
call echo !gmadCommits!!gmadGitHEAD!>>!gmadNameLOG!
call echo. >> !gmadNameLOG!
call "!gmadPathGIT!\git.exe" log -1>>!gmadNameLOG!

timeout 15

IF DEFINED gmadID (
  call "!gmadBinPath!\gmpublish.exe" update -addon "!gmadRevPath!Workshop\!gmadName!.gma" -id "!gmadID!" -icon "!gmadADTools!\pictures\icon.jpg" -changes "Generated by batch">>!gmadNameLOG!
) ELSE (
  call "!gmadBinPath!\gmpublish.exe" create -addon "!gmadRevPath!Workshop\!gmadName!.gma" -icon "!gmadADTools!\pictures\icon.jpg">>!gmadNameLOG!
)

:: Tell the user we are done and clean-up the directory

echo.
rd /S /Q "!gmadRevPath!Workshop"

echo !gmadName! Published !
echo.
timeout 500

:: Give a chance to copy the logs
del "!gmadRevPath!!gmadNameLOG!"

exit 0
